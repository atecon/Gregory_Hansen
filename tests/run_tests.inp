set verbose off
clear

include "./src/gregory_hansen.inp" --force
open "./src/joe96_data.gdt" --quiet


function scalar assert_matrix_equality (matrix actual, matrix expected)
    /* Assert element-wise equality.
    return: int, summed differences; zero value implies "any"-equality  */

    d = abs(vec(actual)) .- abs(vec(expected))

    return sum(d)
end function


bundles Params = null
Params = Params + _(model = 2, choice = 1, adf_maxlag = 2)
Params = Params + _(model = 2, choice = 2, adf_maxlag = 2)
Params = Params + _(model = 2, choice = 3, adf_maxlag = 2)
Params = Params + _(model = 2, choice = 4, adf_maxlag = 2)

Params = Params + _(model = 3, choice = 1, adf_maxlag = 2)
Params = Params + _(model = 3, choice = 2, adf_maxlag = 2)
Params = Params + _(model = 3, choice = 3, adf_maxlag = 2)
Params = Params + _(model = 3, choice = 4, adf_maxlag = 2)

Params = Params + _(model = 4, choice = 1, adf_maxlag = 2)
Params = Params + _(model = 4, choice = 2, adf_maxlag = 2)
Params = Params + _(model = 4, choice = 3, adf_maxlag = 2)
Params = Params + _(model = 4, choice = 4, adf_maxlag = 2)

function void test_model_parameters (const bundles P, const list L)
    print "Start testing model parameter space()."

    matrix y = { L.md }                 # Endogeneous
    matrix x = { L.ynr } ~ { L.sr }     # Exogenous

    loop foreach i P
        # Given
        scalar model = P[i].model
        scalar choice = P[i].choice
        scalar adf_maxlag = P[i].adf_maxlag

        print model choice adf_maxlag

        # When
        bundle Model = estimateGH(y, x, model, choice, adf_maxlag)
    endloop
end function
test_model_parameters(Params, dataset)


function void test_baseline_estimation (const list L)
    print "Start testing function name()"

    # Given
    smpl 1960:1 1990:4                  # Sample selected in the original article
    matrix y = { L.md }                 # Endogeneous
    matrix x = { L.ynr } ~ { L.sr }     # Exogenous

    scalar type_break = 4        # Select break-type: 2=C, 3=C/T, 4=C/S
    scalar choice = 2       # only for ADF*-test relevant
    scalar k = 6            # maximum lag for ADF test

    # When
    bundle model = estimateGH(y, x, type_break, choice, k)

    expected_lag_adf = 4
    expected_tstat_adf = -4.77755
    expected_breakpoint_pct_adf = 0.66129
    expected_breakpoint_pct_za = 0.693548
    expected_breakpoint_pct_zt = 0.693548
    expected_tstat_za = -31.5576
    expected_tstat_zt = -4.18042
    expected_critvalues_adf = {-5.97, -5.73, -5.5, -5.23}
    expected_critvalues_zt = {-68.210, -63.280, -58.330, -52.850}

    # Then
    assert(model.lag_adf == expected_lag_adf)
    assert(model.tstat_adf == expected_tstat_adf)
    assert(model.tstat_zt == expected_tstat_zt)
    assert(model.tstat_za == expected_tstat_za)
    assert(model.breakpoint_pct_adf == expected_breakpoint_pct_adf)
    assert(model.breakpoint_pct_za == expected_breakpoint_pct_za)
    assert(model.breakpoint_pct_zt == expected_breakpoint_pct_zt)

    assert( assert_matrix_equality(model.critvalues_adf, expected_critvalues_adf) == 0)
    assert( assert_matrix_equality(model.critvalues_zt, expected_critvalues_zt) == 0)
end function
test_baseline_estimation(dataset)







function void test_initialization (void)
    print "Start testing function initialization()"

    # Given
    matrix y = seq(1, 3)'
    matrix x = mshape(0, 3, 4)

    # When
    bundle actual = initialization(y, x)
    scalar expected_T = rows(y)
    scalar expected_n_vars = cols(x)

    # Then
    assert(actual.T == expected_T)
    assert(actual.nX == expected_n_vars)
end function
test_initialization()



printf "\nInfo: All tests passed.\n"
quit
